apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

ext {
  androidXVersions = [
    annotation           : '1.0.1',
    appCompat            : '1.1.0',
    cardView             : '1.0.0',
    constraintlayout     : '1.1.3',
    coordinatorlayout    : '1.1.0',
    core                 : '1.2.0',
    experimental         : '1.0.0',
    fragment             : '1.0.0',
    lifecycle            : '2.0.0',
    recyclerView         : '1.0.0',
    recyclerViewSelection: '1.0.0',
    transition           : '1.2.0',
    vectorDrawable       : '1.1.0',
    viewpager2           : '1.0.0',
  ]
}

def compatibility(name) {
  switch (name) {
    case "annotation":
      return "androidx.annotation:annotation:${androidXVersions.annotation}"
    case "appcompat":
      return "androidx.appcompat:appcompat:${androidXVersions.appCompat}"
    case "cardview":
      return "androidx.cardview:cardview:${androidXVersions.cardView}"
    case "constraintlayout":
      return "androidx.constraintlayout:constraintlayout:${androidXVersions.constraintlayout}"
    case "coordinatorlayout":
      return "androidx.coordinatorlayout:coordinatorlayout:${androidXVersions.coordinatorlayout}"
    case "core":
      return "androidx.core:core:${androidXVersions.core}"
    case "fragment":
      return "androidx.fragment:fragment:${androidXVersions.fragment}"
    case "lifecycleRuntime":
      return "androidx.lifecycle:lifecycle-runtime:${androidXVersions.lifecycle}"
    case "recyclerview":
      return "androidx.recyclerview:recyclerview:${androidXVersions.recyclerView}"
    case "transition":
      return "androidx.transition:transition:${androidXVersions.transition}"
    case "vectordrawable":
      return "androidx.vectordrawable:vectordrawable:${androidXVersions.vectorDrawable}"
    case "recyclerViewSelection":
      return "androidx.recyclerview:recyclerview-selection:${androidXVersions.recyclerViewSelection}"
    case "viewpager2":
      return "androidx.viewpager2:viewpager2:${androidXVersions.viewpager2}"
    case "experimental":
      return "androidx.annotation:annotation-experimental:${androidXVersions.experimental}"
    default:
      throw new IllegalArgumentException("No mapping exists for name: $name.")
  }
}

dependencies {
  implementation compatibility("annotation")
  implementation compatibility("appcompat")
  implementation compatibility("cardview")
  implementation compatibility("coordinatorlayout")
  implementation compatibility("core")
  implementation compatibility("experimental")
  implementation compatibility("fragment")
  implementation compatibility("lifecycleRuntime")
  implementation compatibility("recyclerview")
  implementation compatibility("transition")
  implementation compatibility("vectordrawable")
  implementation compatibility("viewpager2")
  implementation 'androidx.core:core-ktx:1.3.2'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

def srcDirs = [
  'com/zeoflow/material/elements/animation',
  'com/zeoflow/material/elements/appbar',
  'com/zeoflow/material/elements/badge',
  'com/zeoflow/material/elements/behavior',
  'com/zeoflow/material/elements/bottomappbar',
  'com/zeoflow/material/elements/bottomnavigation',
  'com/zeoflow/material/elements/bottomsheet',
  'com/zeoflow/material/elements/button',
  'com/zeoflow/material/elements/canvas',
  'com/zeoflow/material/elements/card',
  'com/zeoflow/material/elements/checkbox',
  'com/zeoflow/material/elements/chip',
  'com/zeoflow/material/elements/circularreveal',
  'com/zeoflow/material/elements/circularreveal/cardview',
  'com/zeoflow/material/elements/circularreveal/coordinatorlayout',
  'com/zeoflow/material/elements/color',
  'com/zeoflow/material/elements/colorwheel',
  'com/zeoflow/material/elements/datepicker',
  'com/zeoflow/material/elements/dialog',
  'com/zeoflow/material/elements/drawable',
  'com/zeoflow/material/elements/elevation',
  'com/zeoflow/material/elements/expandable',
  'com/zeoflow/material/elements/floatingactionbutton',
  'com/zeoflow/material/elements/gradientseekbar',
  'com/zeoflow/material/elements/imageview',
  'com/zeoflow/material/elements/internal',
  'com/zeoflow/material/elements/math',
  'com/zeoflow/material/elements/menu',
  'com/zeoflow/material/elements/navigation',
  'com/zeoflow/material/elements/outlined',
  'com/zeoflow/material/elements/pagerindicator',
  'com/zeoflow/material/elements/photoview',
  'com/zeoflow/material/elements/radiobutton',
  'com/zeoflow/material/elements/resources',
  'com/zeoflow/material/elements/ripple',
  'com/zeoflow/material/elements/slidingpanel',
  'com/zeoflow/material/elements/shape',
  'com/zeoflow/material/elements/shadow',
  'com/zeoflow/material/elements/slider',
  'com/zeoflow/material/elements/snackbar',
  'com/zeoflow/material/elements/square',
  'com/zeoflow/material/elements/stateful',
  'com/zeoflow/material/elements/switchmaterial',
  'com/zeoflow/material/elements/tabs',
  'com/zeoflow/material/elements/textfield',
  'com/zeoflow/material/elements/textview',
  'com/zeoflow/material/elements/theme',
  'com/zeoflow/material/elements/theme/overlay',
  'com/zeoflow/material/elements/tooltip',
  'com/zeoflow/material/elements/transition',
  'com/zeoflow/material/elements/transformation',
  'com/zeoflow/material/elements/typography',
  'com/zeoflow/material/elements/viewpager',
]

android {
  sourceSets {
    main.manifest.srcFile 'java/com/zeoflow/material/elements/AndroidManifest.xml'
    main.java.srcDir 'java'
    main.java.includes = srcDirs.collect { it + '/**/*.java' }
    main.java.excludes = [
      '**/build/**',
    ]
    srcDirs.forEach {
      main.res.srcDirs += 'java/' + it + '/res'
      main.res.srcDirs += 'java/' + it + '/res-public'
    }
  }

  testOptions.unitTests.includeAndroidResources = true

  buildTypes.all {
    consumerProguardFiles 'proguard-behaviors.pro', 'proguard-inflater.pro'
  }

  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion
  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode rootProject.ext.versionCode
    versionName rootProject.ext.versionName
    vectorDrawables.useSupportLibrary = true
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  aaptOptions {
    additionalParameters "--no-version-vectors"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  dexOptions {
    preDexLibraries = !project.hasProperty('disablePreDex')
  }
}

apply from: "${rootProject.projectDir}/material-elements/maven-push.gradle"
repositories {
  mavenCentral()
}
